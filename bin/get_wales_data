#!/usr/bin/gawk -f

@load "json"

BEGIN{

  RS="\x04"
  DEBUG = 1
  
  cmd = "curl -s -X POST "                                              \
    "--data 'api_key=74B39D7C-65A5-4CA1-A396AD90F2095398' "             \
    "--data 'method=getCatalogData' "                                   \
    "--data 'pgsz=5000' "                                                 \
    "--data 'cols=collectors,othercatalognumbers,scientific_name,"      \
    "preparators,media,identified_by,square,quadrant,"                  \
    "stratigraphic_position,materials' "                                \
    "--data 'accn_number=UA2020-134' "                                  \
    "'https://arctos.database.museum/component/api/v1/catalog.cfc'"
  
  # "--data 'guid=UAM:Herb:108928' "            \
  
  cmd | getline json
  if (! json::from_json(json, Data)) {
    if (DEBUG)
      walk_array(Data, "Data")
    
    error("API JSON import failed!")
  }

  # walk_array(Data, "Data")

  PROCINFO["sorted_in"] = "@ind_num_asc"
  OFS = "|"
  for (i in Data["Records"]) {
    for (j in Data["Records"][i])
      gsub(/\|/ , ":", Data["Records"][i][j])
    print Data["Records"][i]["guid"],                   \
      Data["Records"][i]["othercatalognumbers"],        \
      Data["Records"][i]["verbatim_date"],              \
      Data["Records"][i]["scientific_name"],            \
      Data["Records"][i]["materials"],                  \
      Data["Records"][i]["spec_locality"],                        \
      Data["Records"][i]["dec_lat"],                              \
      Data["Records"][i]["dec_long"],                             \
      Data["Records"][i]["coordinateuncertaintyinmeters"],        \
      Data["Records"][i]["stratigraphic_position"],               \
      Data["Records"][i]["quadrant"],                             \
      Data["Records"][i]["square"],                               \
      Data["Records"][i]["collectors"],                           \
      Data["Records"][i]["identified_by"],                        \
      Data["Records"][i]["preparators"],                          \
      Data["Records"][i]["media"]
  }
  
  if (Data["Result"] != "OK") {
    exit 1
  }
  else
    exit 0

}


function error(msg) {
  print "Error: " msg > "/dev/stderr"
  exit 1
}

function walk_array(arr, name,      i, s) {
  for (i in arr) {
    if (isarray(arr[i]))
      walk_array(arr[i], (name "[" i "]"))
    else {
      # printf("%s[%s] = %s\n", name, i, arr[i])
      s = sprintf("%s[%s] = %s", name, i, arr[i])
      print gensub(/\[([^0-9][^]]*)\]/,"[\"\\1\"]","G",s)
    }
  }
}
