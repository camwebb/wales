#!/usr/bin/gawk -f

@load "json"

BEGIN{

  DEBUG = 1
  get("UA2020-134")
  get("UA2020-135")
  get("UA2020-136")

}

function get(accn,        cmd, json, data) {
  print "Getting " accn > "/dev/stderr"
  
  cmd = "curl -s -X POST "                                              \
    "--data 'api_key=74B39D7C-65A5-4CA1-A396AD90F2095398' "             \
    "--data 'method=getCatalogData' "                                   \
    "--data 'pgsz=5000' "                                                 \
    "--data 'cols=collectors,othercatalognumbers,scientific_name,"      \
    "preparators,media,identified_by,square,quadrant,"                  \
    "stratigraphic_position,materials' "                                \
    "--data 'accn_number=" accn "' "                                    \
    "'https://arctos.database.museum/component/api/v1/catalog.cfc'"
  
  # "--data 'guid=UAM:Herb:108928' "            \
  
  cmd | getline json
  if (! json::from_json(json, data)) {
    if (DEBUG)
      walk_array(data, "data")
    
    error("API JSON import failed!")
  }

  # walk_array(data, "data")

  PROCINFO["sorted_in"] = "@ind_num_asc"
  OFS = "|"
  for (i in data["Records"]) {
    for (j in data["Records"][i])
      gsub(/\|/ , ":", data["Records"][i][j])
    print data["Records"][i]["guid"],                       \
      gensub(/.*AHRS \(Alaska Heritage Resources Survey\)=([^,]+),.*/, \
             "\\1", "G", data["Records"][i]["othercatalognumbers"]),   \
      gensub(/.*Locality ID=([^,]+),.*/, \
             "\\1", "G", data["Records"][i]["othercatalognumbers"]),   \
      data["Records"][i]["verbatim_date"],                  \
      data["Records"][i]["scientific_name"],                \
      data["Records"][i]["materials"],                            \
      data["Records"][i]["spec_locality"],                        \
      data["Records"][i]["dec_lat"],                              \
      data["Records"][i]["dec_long"],                             \
      data["Records"][i]["coordinateuncertaintyinmeters"],        \
      data["Records"][i]["stratigraphic_position"],               \
      data["Records"][i]["quadrant"],                             \
      data["Records"][i]["square"],                               \
      data["Records"][i]["collectors"],                           \
      data["Records"][i]["identified_by"],                        \
      data["Records"][i]["preparators"],                          \
      data["Records"][i]["media"]
  }
  
  if (data ["Result"] != "OK") {
    return 1
  }
  else
    return 0

}


function error(msg) {
  print "Error: " msg > "/dev/stderr"
  exit 1
}

function walk_array(arr, name,      i, s) {
  for (i in arr) {
    if (isarray(arr[i]))
      walk_array(arr[i], (name "[" i "]"))
    else {
      # printf("%s[%s] = %s\n", name, i, arr[i])
      s = sprintf("%s[%s] = %s", name, i, arr[i])
      print gensub(/\[([^0-9][^]]*)\]/,"[\"\\1\"]","G",s)
    }
  }
}
